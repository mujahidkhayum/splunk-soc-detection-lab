index=lab sourcetype="XmlWinEventLog:Security" earliest=-15m latest=now "<EventID>4625</EventID>"
| rex field=_raw "<Data Name='TargetUserName'>(?<TargetUserName>[^<]+)</Data>"
| rex field=_raw "<Data Name='IpAddress'>(?<IpAddress>[^<]+)</Data>"
| rex field=_raw "<Data Name='LogonType'>(?<LogonType>\d+)</Data>"
| rex field=_raw "<Data Name='Status'>(?<Status>0x[0-9A-Fa-f]+)</Data>"
| rex field=_raw "<Data Name='SubStatus'>(?<SubStatus>0x[0-9A-Fa-f]+)</Data>"
| eval IpAddress=coalesce(IpAddress,"")
| where LogonType="3"
  AND IpAddress!="" AND IpAddress!="-" AND IpAddress!="127.0.0.1" AND IpAddress!="::1" AND NOT like(IpAddress,"fe80:%")
| stats count as fails values(Status) as Status values(SubStatus) as SubStatus by TargetUserName IpAddress host LogonType
| where fails>=5
| sort - fails
